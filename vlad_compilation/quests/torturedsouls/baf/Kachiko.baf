//IF
//	Global("VP_Kachiko_Yoshimo","GLOBAL",1)
//THEN
//	RESPONSE #100
//		SetGlobal("VP_Kachiko_Yoshimo","GLOBAL",2)
//		MoveGlobal("AR0313",Myself,[784.333])
//END

//IF
//	Global("VP_Kachiko_Yoshimo","GLOBAL",3)
//THEN
//	RESPONSE #100
//		SetGlobal("VP_Kachiko_Yoshimo","GLOBAL",4)
//		MoveGlobal("TT0102",Myself,[165.727])
//END

//IF
//	OR(2)
//		Global("VP_Kachiko_Joins1","GLOBAL",1)
//		Global("VP_Kachiko_Joins2","GLOBAL",1)
//THEN
//	RESPONSE #100
//		StartDialogueNoSet([PC])
//END

// Yoshimo is wounded or dead

IF
	InParty(Myself)
	InParty("Yoshimo")
	HPPercentLT("Yoshimo",20)
	!Dead("Yoshimo")
	!CombatCounter(0)
	Exists(LastAttackerOf("Yoshimo"))
	Allegiance(LastAttackerOf("Yoshimo"),ENEMY)
	Global("VP_Yoshimo_Dies","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("VP_Yoshimo_Dies","GLOBAL",1)
		EquipMostDamagingMelee()
		DisplayStringHead(Myself,~Yoshimo! Nooooo!!!~) //
		MoveToObjectNoInterrupt(LastAttackerOf("Yoshimo"))
		AttackReevaluate(LastAttackerOf("Yoshimo"),75)
		Continue()
END

IF
	InParty(Myself)
	Dead("Yoshimo")
	CombatCounter(0)
	Global("VP_Yoshimo_Is_Dead","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("VP_Yoshimo_Is_Dead","GLOBAL",1)
		SetGlobal("VP_Yoshimo_Dies","GLOBAL",2)
END

IF
	InParty(Myself)
	CombatCounter(0)
	See(Player1)
	!StateCheck(Player1,STATE_SLEEPING)
	Global("VP_Yoshimo_Dies","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("VP_Yoshimo_Dies","GLOBAL",3)
		StartDialogueNoSet([PC])
END

IF
	InParty(Myself)
	InParty("Yoshimo")
	!Dead("Yoshimo")
	Global("VP_Yoshimo_Dies","GLOBAL",1)
	CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("VP_Yoshimo_Dies","GLOBAL",0)
END

// First Meeting

IF
	See("Yoshimo")
	Global("VP_Kachiko_Meet","GLOBAL",0)
	Global("VP_YoshimoSearch","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("VP_Kachiko_Meet","GLOBAL",1)
		Dialogue("Yoshimo")
END

IF
	See([PC])
	!InParty("Yoshimo")
	!See("Yoshimo")
	NumTimesTalkedTo(0)
	Global("VP_Kachiko_Meet","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("VP_Kachiko_Meet","GLOBAL",1)
		Dialogue(LastSeenBy(Myself))
END

IF
	See([PC])
	InParty("Yoshimo")
	!See("Yoshimo")
	NumTimesTalkedTo(0)
	Global("VP_Yoshimo_Invisible","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("VP_Yoshimo_Invisible","GLOBAL",1)
		DisplayStringHead(Myself,~I'll find this bastard Yoshimo! I promise I'll find him!~) //
END

IF
	See([PC])
	InParty("Yoshimo")
	!See("Yoshimo")
	NumTimesTalkedTo(0)
	Global("VP_Yoshimo_Invisible","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("VP_Yoshimo_Invisible","GLOBAL",2)
		Wait(5)
		DisplayStringHead(Myself,~Bring me Yoshimo!~) // 
END

// Searching Yoshimo

IF
	InParty(Myself)
	AreaCheck("AR0700")
	Global("VP_YoshimoSearch","GLOBAL",1)
	Global("VP_FindYoshi","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("VP_FindYoshi","LOCALS",1)
		SetGlobalTimer("VP_FindYoshiTimer","GLOBAL",THREE_DAYS)
END

IF
	GlobalTimerExpired("VP_FindYoshiTimer","GLOBAL")
	Global("VP_YoshimoSearch","GLOBAL",1)
	!InParty("Yoshimo")
	!See("Yoshimo")
	CombatCounter(0)
	InParty(Myself)
	See(Player1)
	!StateCheck(Player1,STATE_SLEEPING)
THEN
	RESPONSE #100
		SetGlobal("VP_YoshimoSearch","GLOBAL",2)
		StartDialogueNoSet(Player1)
END

IF
	Global("VP_YoshimoSearch","GLOBAL",3)
	GlobalTimerExpired("VP_KachikoLeaves","GLOBAL")
	!InParty("Yoshimo")
	!See("Yoshimo")
	CombatCounter(0)
	InParty(Myself)
	See(Player1)
	!StateCheck(Player1,STATE_SLEEPING)
THEN
	RESPONSE #100
		SetGlobal("VP_YoshimoSearch","GLOBAL",4)
		StartDialogueNoSet(Player1)
END

//IF
//  InParty(Myself)
//  !InParty("Yoshimo")
//  See("Yoshimo")
// GlobalGT("VP_YoshimoSearch","GLOBAL",0)
//  GlobalLT("VP_YoshimoSearch","GLOBAL",4)
//THEN
//  RESPONSE #100
//    SetGlobal("VP_YoshimoSearch","GLOBAL",5)
//    Dialogue("Yoshimo")
//END

//Yoshimo is Found

IF
	InParty(Myself)
	!InParty("Yoshimo")
	See([PC])
	See("Yoshimo")
	GlobalLT("VP_YoshimoSearch","GLOBAL",5)
THEN
	RESPONSE #100
		SetGlobal("VP_YoshimoSearch","GLOBAL",5)
		StartCutSceneMode()
		ClearAllActions()
		Wait(1)
		Dialogue("Yoshimo")
		EndCutSceneMode()
END

IF
	!InParty(Myself)
	InParty("Yoshimo")
	Detect("Yoshimo")
	Global("VP_KachikoInTavern","GLOBAL",1)
	Global("VP_YoshimoDetected","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("VP_YoshimoDetected","GLOBAL",1)
		StartCutSceneMode()
		ClearAllActions()
		Wait(1)
		Dialogue("Yoshimo")
		EndCutSceneMode()
END

// Kachiko-Yoshimo Romance

IF
	InParty(Myself)
	InParty("Yoshimo")
	!Dead("Yoshimo")
	Global("VP_KachYosh_Romance","GLOBAL",0)
	GlobalLT("Chapter","GLOBAL",4)
	!Global("VP_OnIsland","GLOBAL",1)
	Global("LoveTalk","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobalTimer("VP_KachikoRomance","GLOBAL",ONE_DAY)
		SetGlobal("VP_KachYosh_Romance","GLOBAL",1)
END

IF
	InParty(Myself)
	InParty("Yoshimo")
	See("Yoshimo")
	!Dead("Yoshimo")
	!StateCheck("Yoshimo",STATE_SLEEPING)
	CombatCounter(0)
	!See([ENEMY])
	GlobalTimerExpired("VP_KachikoRomance","GLOBAL")
	GlobalLT("Chapter","GLOBAL",4)
	!Global("VP_OnIsland","GLOBAL",1)
	OR(2)
		Global("LoveTalk","LOCALS",4)
		Global("LoveTalk","LOCALS",6)
THEN
	RESPONSE #100
		IncrementGlobal("LoveTalk","LOCALS",1)
		SetGlobalTimer("VP_KachikoRomance","GLOBAL",TWO_DAYS)
		PlaySong(999999)
		Interact("Yoshimo")
END

IF
	InParty(Myself)
	Global("VP_YoshimoExcuse","GLOBAL",4)
	Global("Kachiko_Saved","GLOBAL",2)
	See("Yoshimo")
	InParty("Yoshimo")
	!Dead("Yoshimo")
THEN
	RESPONSE #100
		SetGlobal("VP_YoshimoExcuse","GLOBAL",5)
		SetGlobalTimer("VP_Kachiko_LastTalk","GLOBAL",ONE_DAY)
END

IF
	InParty(Myself)
	GlobalTimerExpired("VP_Kachiko_LastTalk","GLOBAL")
	Global("VP_YoshimoExcuse","GLOBAL",5)
	InParty(Myself)
	See("Yoshimo")
	InParty("Yoshimo")
	!Dead("Yoshimo")
	!StateCheck("Yoshimo",STATE_SLEEPING)
	CombatCounter(0)
	!Range([NEUTRAL],10)
	PartyRested()
THEN
	RESPONSE #100
		SetGlobal("VP_YoshimoExcuse","GLOBAL",6)
		PlaySong(999999)
		Interact("Yoshimo")
END

// Banters

IF
	InParty(Myself)
	InParty("Anomen")
	InParty("Yoshimo")
	!Dead("Anomen")
	!Dead("Yoshimo")
	Global("VP_BKachiko_1","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_1","LOCALS",1)
		SetGlobalTimer("VP_Kachiko_Anomen","GLOBAL",ONE_DAY)
END

IF
	GlobalTimerExpired("VP_Kachiko_Anomen","GLOBAL")
	InParty(Myself)
	InParty("Anomen")
	InParty("Yoshimo")
	See("Anomen")
	See("Yoshimo")
	!Dead("Anomen")
	!Dead("Yoshimo")
	!StateCheck("Anomen",STATE_SLEEPING)
	!StateCheck("Yoshimo",STATE_SLEEPING)
	CombatCounter(0)
	Global("AnomenMatch","GLOBAL",1)
	Global("VP_BKachiko_1","LOCALS",1)
	!See([ENEMY])
	!Range([NEUTRAL],10)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_1","LOCALS",2)
		Interact("Anomen")
END

IF
	InParty(Myself)
	InParty("Valygar")
	!Dead("Valygar")
	Global("VP_BKachiko_2","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_2","LOCALS",1)
		SetGlobalTimer("VP_Kachiko_Valygar","GLOBAL",TWO_DAYS)
END

IF
	GlobalTimerExpired("VP_Kachiko_Valygar","GLOBAL")
	InParty(Myself)
	InParty("Valygar")
	See("Valygar")
	!Dead("Valygar")
	!StateCheck("Valygar",STATE_SLEEPING)
	CombatCounter(0)
	Global("VP_BKachiko_2","LOCALS",1)
	!See([ENEMY])
	!Range([NEUTRAL],10)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_2","LOCALS",2)
		Interact("Valygar")
END

IF
	InParty(Myself)
	InParty("Minsc")
	!Dead("Minsc")
	Global("VP_BKachiko_3","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_3","LOCALS",1)
		SetGlobalTimer("VP_Kachiko_Minsc","GLOBAL",FOUR_DAYS)
END

IF
	GlobalTimerExpired("VP_Kachiko_Minsc","GLOBAL")
	InParty(Myself)
	InParty("Minsc")
	See("Minsc")
	!Dead("Minsc")
	!StateCheck("Minsc",STATE_SLEEPING)
	CombatCounter(0)
	Global("VP_BKachiko_3","LOCALS",1)
	!See([ENEMY])
	!Range([NEUTRAL],10)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_3","LOCALS",2)
		Interact("Minsc")
END

IF
	InParty(Myself)
	InParty("Nalia")
	InParty("Yoshimo")
	!Dead("Nalia")
	!Dead("Yoshimo")
	Global("VP_BKachiko_4","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_4","LOCALS",1)
		SetGlobalTimer("VP_Kachiko_Nalia","GLOBAL",FOUR_DAYS)
END

IF
	GlobalTimerExpired("VP_Kachiko_Nalia","GLOBAL")
	InParty(Myself)
	InParty("Nalia")
	InParty("Yoshimo")
	See("Nalia")
	See("Yoshimo")
	!Dead("Nalia")
	!Dead("Yoshimo")
	!StateCheck("Nalia",STATE_SLEEPING)
	!StateCheck("Yoshimo",STATE_SLEEPING)
	CombatCounter(0)
	Global("VP_BKachiko_4","LOCALS",1)
	!See([ENEMY])
	!Range([NEUTRAL],10)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_4","LOCALS",2)
		Interact("Nalia")
END

IF
	InParty(Myself)
	InParty("Viconia")
	!Dead("Viconia")
	Global("VP_BKachiko_5","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_5","LOCALS",1)
		SetGlobalTimer("VP_Kachiko_Viconia","GLOBAL",FIVE_DAYS)
END

IF
	GlobalTimerExpired("VP_Kachiko_Viconia","GLOBAL")
	InParty(Myself)
	InParty("Viconia")
	See("Viconia")
	!Dead("Viconia")
	!StateCheck("Viconia",STATE_SLEEPING)
	CombatCounter(0)
	Global("VP_BKachiko_5","LOCALS",1)
	!See([ENEMY])
	!Range([NEUTRAL],10)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_5","LOCALS",2)
		Interact("Viconia")
END

IF
	InParty(Myself)
	InParty("HaerDalis")
	InParty("Yoshimo")
	!Dead("HaerDalis")
	!Dead("Yoshimo")
	Global("VP_BKachiko_6","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_6","LOCALS",1)
		SetGlobalTimer("VP_Kachiko_HaerDalis","GLOBAL",SIX_DAYS)
END

IF
	GlobalTimerExpired("VP_Kachiko_HaerDalis","GLOBAL")
	InParty(Myself)
	InParty("HaerDalis")
	InParty("Yoshimo")
	See("HaerDalis")
	See("Yoshimo")
	!Dead("HaerDalis")
	!Dead("Yoshimo")
	!StateCheck("HaerDalis",STATE_SLEEPING)
	!StateCheck("Yoshimo",STATE_SLEEPING)
	CombatCounter(0)
	Global("VP_BKachiko_6","LOCALS",1)
	!See([ENEMY])
	!Range([NEUTRAL],10)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_6","LOCALS",2)
		Interact("HaerDalis")
END

/*IF
	InParty(Myself)
	InParty("Keldorn")
	!Dead("Keldorn")
	Global("VP_BKachiko_7","LOCALS",0)
	Global("VP_Kr_Funneral","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_7","LOCALS",1)
		SetGlobalTimer("VP_Kachiko_Keldorn","GLOBAL",SEVEN_DAYS)
END

IF
	GlobalTimerExpired("VP_Kachiko_Keldorn","GLOBAL")
	InParty(Myself)
	InParty("Keldorn")
	See("Keldorn")
	!Dead("Keldorn")
	!StateCheck("Keldorn",STATE_SLEEPING)
	CombatCounter(0)
	PartyRested()
	Global("VP_BKachiko_7","LOCALS",1)
	!See([ENEMY])
	!Range([NEUTRAL],10)
	Global("VP_Kr_Funneral","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_7","LOCALS",2)
		Interact("Keldorn")
END*/

IF
	InParty(Myself)
	InParty("Aerie")
	!Dead("Aerie")
	Global("VP_BKachiko_8","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_8","LOCALS",1)
		SetGlobalTimer("VP_Kachiko_Aerie","GLOBAL",EIGHT_DAYS)
END

IF
	GlobalTimerExpired("VP_Kachiko_Aerie","GLOBAL")
	InParty(Myself)
	InParty("Aerie")
	See("Aerie")
	!Dead("Aerie")
	!StateCheck("Aerie",STATE_SLEEPING)
	CombatCounter(0)
	Global("VP_BKachiko_8","LOCALS",1)
	!See([ENEMY])
	!Range([NEUTRAL],10)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_8","LOCALS",2)
		Interact("Aerie")
END

IF
	InParty(Myself)
	InParty("Jaheira")
	!Dead("Jaheira")
	InParty("Yoshimo")
	!Dead("Yoshimo")
	Global("VP_BKachiko_9","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_9","LOCALS",1)
		SetGlobalTimer("VP_Kachiko_Jaheira","GLOBAL",NINE_DAYS)
END

IF
	GlobalTimerExpired("VP_Kachiko_Jaheira","GLOBAL")
	InParty(Myself)
	InParty("Jaheira")
	InParty("Yoshimo")
	See("Jaheira")
	See("Yoshimo")
	!Dead("Yoshimo")
	!Dead("Jaheira")
	!StateCheck("Jaheira",STATE_SLEEPING)
	!StateCheck("Yoshimo",STATE_SLEEPING)
	CombatCounter(0)
	Global("Start_Island","GLOBAL",0)
	Global("VP_BKachiko_9","LOCALS",1)
	!See([ENEMY])
	!Range([NEUTRAL],10)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_9","LOCALS",2)
		Interact("Jaheira")
END

IF
	InParty(Myself)
	InParty("Edwin")
	!Dead("Edwin")
	InParty("Yoshimo")
	!Dead("Yoshimo")
	Global("VP_BKachiko_10","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_10","LOCALS",1)
		SetGlobalTimer("VP_Kachiko_Edwin","GLOBAL",TEN_DAYS)
END

IF
	GlobalTimerExpired("VP_Kachiko_Edwin","GLOBAL")
	InParty(Myself)
	InParty("Edwin")
	InParty("Yoshimo")
	See("Edwin")
	See("Yoshimo")
	!Dead("Edwin")
	!Dead("Yoshimo")
	!StateCheck("Edwin",STATE_SLEEPING)
	!StateCheck("Yoshimo",STATE_SLEEPING)
	CombatCounter(0)
	Global("VP_BKachiko_10","LOCALS",1)
	!See([ENEMY])
	!Range([NEUTRAL],10)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_10","LOCALS",2)
		Interact("Edwin")
END

IF
	InParty(Myself)
	InParty("Imoen2")
	!Dead("Imoen2")
	Global("VP_BKachiko_11","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_11","LOCALS",1)
		SetGlobalTimer("VP_Kachiko_Imoen","GLOBAL",ONE_DAY)
END

IF
	GlobalTimerExpired("VP_Kachiko_Imoen","GLOBAL")
	InParty(Myself)
	InParty("Imoen2")
	See("Imoen2")
	!Dead("Imoen2")
	!StateCheck("Imoen2",STATE_SLEEPING)
	CombatCounter(0)
	GlobalGT("AsylumPlot","GLOBAL",55)
	Global("VP_BKachiko_11","LOCALS",1)
	!See([ENEMY])
	!Range([NEUTRAL],10)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_11","LOCALS",2)
		Interact("Imoen2")
END

IF
	InParty(Myself)
	InParty("Mazzy")
	InParty("Valygar")
	!Dead("Mazzy")
	!Dead("Valygar")
	Global("VP_BKachiko_12","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_12","LOCALS",1)
		SetGlobalTimer("VP_Kachiko_Mazzy","GLOBAL",ELEVEN_DAYS)
END

IF
	GlobalTimerExpired("VP_Kachiko_Mazzy","GLOBAL")
	InParty(Myself)
	InParty("Mazzy")
	InParty("Valygar")
	See("Mazzy")
	See("Valygar")
	!Dead("Mazzy")
	!Dead("Valygar")
	!StateCheck("Mazzy",STATE_SLEEPING)
	!StateCheck("Valygar",STATE_SLEEPING)
	CombatCounter(0)
	PartyRested()
	Global("VP_BKachiko_12","LOCALS",1)
	!See([ENEMY])
	!Range([NEUTRAL],10)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_12","LOCALS",2)
		Interact("Mazzy")
END

IF
	InParty(Myself)
	InParty("Jan")
	!Dead("Jan")
	!Global("End_Island","GLOBAL",0)
	Global("VP_BKachiko_13","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_13","LOCALS",1)
		SetGlobalTimer("VP_Kachiko_Jan","GLOBAL",TWELVE_DAYS)
END

IF
	GlobalTimerExpired("VP_Kachiko_Jan","GLOBAL")
	InParty(Myself)
	InParty("Jan")
	See("Jan")
	!Dead("Jan")
	!StateCheck("Jan",STATE_SLEEPING)
	CombatCounter(0)
	!Global("End_Island","GLOBAL",0)
	Global("VP_BKachiko_13","LOCALS",1)
	!See([ENEMY])
	!Range([NEUTRAL],10)
THEN
	RESPONSE #100
		SetGlobal("VP_BKachiko_13","LOCALS",2)
		Interact("Jan")
END

// Fight Blocks

IF
	Allegiance(Myself,ENEMY)
	!InParty(Myself)
	Global("NPCFight","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("NPCFight","LOCALS",1)
		ChangeAIScript("",DEFAULT)
		ChangeAIScript("useitem",GENERAL)
		ChangeAIScript("kachix",SPECIFICS)
		SetDialog("")
		Continue()
END

IF
	Allegiance(Myself,NEUTRAL)
	AttackedBy([GOODCUTOFF],DEFAULT)
THEN
	RESPONSE #100
		ChangeAIScript("kachix",SPECIFICS)
		ChangeAIScript("useitem",GENERAL)
		ChangeAIScript("",DEFAULT)
		Enemy()
		Continue()
END

IF
	!Allegiance(Myself,ENEMY)
	!InParty(Myself)
	Global("NPCFight","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("NPCFight","LOCALS",0)
		ChangeAIScript("",GENERAL)
		ChangeAIScript("",SPECIFICS)
		Continue()
END

IF
	Global("MyHome_Attacked","GLOBAL",1)
	Global("Hashimoto_Attacked","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("Hashimoto_Attacked","LOCALS",1)
		LeaveParty()
		Enemy()
END