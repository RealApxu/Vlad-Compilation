IF
	HPPercentLT(Myself,50)
	HasItem("potn55",Myself) // Potion of Superior Healing
THEN
	RESPONSE #100
		DisplayStringHead(Myself,46150) // quaffs a potion
		UseItem("potn55",Myself) // Potion of Superior Healing
		Continue()
END

IF
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_HASTED)
	HasItem("potn14",Myself) // Oil of Speed
THEN
	RESPONSE #100
		DisplayStringHead(Myself,46150) // quaffs a potion
		UseItem("potn14",Myself) // Oil of Speed
		Continue()
END

IF
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	!StateCheck(Myself,STATE_INVISIBLE)
	HasItem("potn10",Myself) // Potion of Invisibility
THEN
	RESPONSE #100
		DisplayStringHead(Myself,46150) // quaffs a potion
		UseItem("potn10",Myself) // Potion of Invisibility
		Continue()
END

IF
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	!StateCheck(Myself,STATE_INVISIBLE)
	HasItem("ring05",Myself) // Ring of Invisibility 
THEN
	RESPONSE #100
		UseItem("ring05",Myself) // Ring of Invisibility 
		Continue()
END

IF
	AttackedBy([GOODCUTOFF],DEFAULT)
	Allegiance(Myself,NEUTRAL)
THEN
	RESPONSE #100
		Shout(89)
		Enemy()
END

IF
	HitBy([GOODCUTOFF],CRUSHING)
THEN
	RESPONSE #100
		Shout(89)
		Continue()
END

IF
	OR(5)
		AttackedBy([PC],DEFAULT)
		AttackedBy([FAMILIAR],DEFAULT)
		AttackedBy([ALLY],DEFAULT)
		AttackedBy([CONTROLLED],DEFAULT)
		AttackedBy([CHARMED],DEFAULT)
	Allegiance(Myself,ENEMY)
	ActionListEmpty()
THEN
	RESPONSE #100
		Shout(89)
		Continue()
END

IF
	Heard([ANYONE],89)
	Allegiance(Myself,NEUTRAL)
	!Class(Myself,INNOCENT)
THEN
	RESPONSE #100
		Enemy()
		MoveToObject(LastHeardBy(Myself))
END

IF
	Heard([ANYONE],89)
	Allegiance(Myself,NEUTRAL)
THEN
	RESPONSE #100
		Enemy()
END

IF
	Heard([ANYONE],89)
	!Class(Myself,INNOCENT)
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		MoveToObject(LastHeardBy(Myself))
		Continue()
END


