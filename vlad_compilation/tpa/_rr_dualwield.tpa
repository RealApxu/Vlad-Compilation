//////////////////////////////////////////////////////////////////////\
// Rogue ReBalancing - Proper Dual Wielding
//////////////////////////////////////////////////////////////////////\

/*
Old version removed, new version added. Thanks to aVENGER!
COPY_EXISTING ~WEAPPROF.2DA~ ~override~
SET_2DA_ENTRY 34 7 1 ~3~
SET_2DA_ENTRY 34 8 1 ~3~
SET_2DA_ENTRY 34 16 1 ~3~
SET_2DA_ENTRY 34 18 1 ~3~
SET_2DA_ENTRY 34 19 1 ~3~
SET_2DA_ENTRY 31 20 1 ~2~
SET_2DA_ENTRY 34 20 1 ~3~
SET_2DA_ENTRY 31 21 1 ~2~
SET_2DA_ENTRY 34 21 1 ~3~
SET_2DA_ENTRY 34 39 1 ~3~
SET_2DA_ENTRY 34 40 1 ~3~
SET_2DA_ENTRY 34 43 1 ~3~
SET_2DA_ENTRY 34 44 1 ~3~
BUT_ONLY
*/

DEFINE_ACTION_FUNCTION rogue_twf BEGIN
    OUTER_SET count = 0

    ACTION_FOR_EACH class IN
                    thief
                    bard
                    fighter_thief
                    fighter_mage_thief
                    mage_thief
                    cleric_thief
    BEGIN
        OUTER_SPRINT $rogues("%count%") "%class%"
        OUTER_SET ++count
    END

    COPY_EXISTING kitlist.2da override
        READ_2DA_ENTRIES_NOW kitlist 3
        FOR (i = 2; i < kitlist; ++i) BEGIN // Skip past the header and RESERVE rows
            READ_2DA_ENTRY_FORMER kitlist i 1 label
            READ_2DA_ENTRY_FORMER kitlist i 8 class
            PATCH_IF class = 4 OR class = 5 BEGIN // thief OR bard
                SPRINT $rogues("%count%") "%label%"
                ++count
            END
        END
    BUT_ONLY

    COPY_EXISTING ~WEAPPROF.2DA~ override
        READ_2DA_ENTRIES_NOW weapprof 3
        COUNT_2DA_COLS cols
        PHP_EACH rogues AS _ => symbol BEGIN
            FOR (i = 0; i < cols - 1; ++i) BEGIN
                READ_2DA_ENTRY_FORMER weapprof 0 i column_name
                PATCH_IF "%column_name%" STRING_EQUAL_CASE "%symbol%" BEGIN
                    SET_2DA_ENTRY_LATER wweapprof 32 i + 1 3 // 3 stars in two-weapon style
                END
            END
        END
        SET_2DA_ENTRIES_NOW wweapprof 3
        PRETTY_PRINT_2DA
    BUT_ONLY
END

LAF rogue_twf END